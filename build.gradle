buildscript {
    ext{
        pluginRepository = "http://10.13.3.17:18081/repository/maven-thirdparty"
        bitsVersion = "1.0.0-SNAPSHOT"
        wadeVersion = "5.5.1-SNAPSHOT"
        springVersion = "2.1.12.RELEASE"
        runtimeConfigDir = System.getProperty("runtimeConfigDir")
        distDir = System.getProperty("distDir")
    }
    repositories {
        mavenLocal()
        maven { url "${pluginRepository}" }
    }
    dependencies {
        classpath("io.freefair.gradle:lombok-plugin:4.1.6")
        classpath("io.spring.gradle:dependency-management-plugin:1.0.9.RELEASE");
    }
}

def env=System.getProperty("profile") ?:"dev"
if (env == 'prd') {
    apply from:"config-prd.gradle"
} else {
    apply from:"config-dev.gradle"
}

apply plugin: 'idea'

project.configurations.all {
    resolutionStrategy {
        // check for updates
        cacheChangingModulesFor 2, 'minutes'
    }
}

subprojects {

    buildscript {
        repositories {
            mavenLocal()
            maven { url "${pluginRepository}" }
        }
        dependencies {
            classpath("io.freefair.gradle:lombok-plugin:4.1.6")
            classpath("io.spring.gradle:dependency-management-plugin:1.0.9.RELEASE");
        }
    }

    //引入插件
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "io.freefair.lombok"
    apply plugin: 'java'

    repositories {
        mavenLocal()
        maven { url "${publicRepository}" }
    }

    //发布配置
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                //artifact sourcesJar
                //artifact javadocJar

                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    groupId rootProject.group
                    artifactId project.name
                    version rootProject.version
                    name = project.name
                    description = project.name
                    url = 'http://git.wadecn.com:18084/south-china/base'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    scm {
                        connection = 'scm:git:http://git.wadecn.com:18084/south-china/base'
                        developerConnection = 'scm:git:http://git.wadecn.com:18084/south-china/base.git'
                        url = 'http://git.wadecn.com:18084/south-china/base'
                    }
                }
            }
        }
        repositories {
            maven {
                name "ZdMavenDev"
                url "http://10.13.3.17:18081/repository/maven-dev/"
                credentials {
                    username "dev"
                    password '1q1w1e1r'
                }
            }
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.compilerArgs << '-parameters'
    }

    task releaseJar(type: Copy){
        if( null == distDir || "".equals(distDir.trim()) ){
            return;
        }
        from 'build/libs'
        into "${distDir}/jars"
        exclude "*-BOOT.jar"
    }

    jar.finalizedBy("releaseJar")

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    /**
     * 构建时跳过test
     */
    test.onlyIf { "true".equals(System.getProperty("widthTest")) }

    //公共依赖
    dependencies {
        runtimeOnly "log4j:log4j"

        annotationProcessor "org.mapstruct:mapstruct-processor"
        compileOnly "org.mapstruct:mapstruct-processor"
        testAnnotationProcessor "org.mapstruct:mapstruct-processor"
        testCompileOnly "org.mapstruct:mapstruct-processor"
    }
}