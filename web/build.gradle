buildscript {
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springVersion}")
    }
}

group = rootProject.group
version = rootProject.version

apply plugin: 'org.springframework.boot'
//apply plugin: 'war'

task releaseBootJar(type: Copy){
    if( null == distDir || "".equals(distDir.trim()) ){
        return;
    }
    from 'build/libs'
    into "${distDir}/tars"
    include "*-BOOT.jar"
}

processResources {
    if( null == runtimeConfigDir || "".equals(runtimeConfigDir.trim()) ){
        return;
    }
    from "${runtimeConfigDir}/web/${rootProject.name.replace("crm-", "")}/config"
    //生成release.txt
    File file = file("${temporaryDir}/release.txt");
    file.text = "number=" + new Date().format("yyMMddHHmmss");
    from file;
}

bootJar {
    classifier = 'BOOT'
    excludeDevtools = true

    bootInf {
        from "${distDir}/jars"
        into "lib"
        includes = ['*-pub-*.jar', '*-web-*.jar']
        excludes = ["*-BOOT.jar", "${rootProject.name}-*.jar"]
    }

    finalizedBy("releaseBootJar")
}

//bootWar {
//    excludeDevtools = true
//    requiresUnpack '**/*tomcat*.jar'
//}


jar {
    includes = [
            "META-INF/**",
            "com/asiainfo/**",
            "export/**",
            "import/**",
            "static/**",
            "tapestry/**",
            "i18n.*"
    ]

    excludes = [
            //"com/asiainfo/crm/*/web/config/**",
            "com/asiainfo/crm/*/web/controller/**",
            "com/asiainfo/crm/*/web/WebApplication.*",
    ]

    enabled = true
}

artifacts {
    archives jar
}

sourceSets.main.resources {
    srcDirs = [
            'src/main/resources',
            'src/main/java',
    ]
}

/**
 * dependencyManagement 只能放在各子工程构建脚本里，不能放在rootProject的subproject里，否则会出现jar包版本处理不正确的情况
 */
dependencyManagement {
    imports {
        mavenBom "com.asiainfo.bits:bits-dependencies:${bitsVersion}"
    }
}

dependencies {

    compile("com.asiainfo.bits:bits-web-framework:${bitsVersion}")
    compile("com.asiainfo.bits:bits-web-touchframe:${bitsVersion}")
    compile "com.asiainfo.bits:bits-csf:${bitsVersion}"

    compile project(":${rootProject.name}-pub")
    compile project(":${rootProject.name}-api")
}
